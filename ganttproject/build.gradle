buildscript {
    ext.kotlin_version = '1.3.+'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.openjfx:javafx-plugin:0.0.+'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.openjfx.javafxplugin'


ext {
    libDir = 'lib/core'
}

repositories {
    mavenCentral()
    jcenter()
}

javafx {
    modules = [ 'javafx.controls', 'javafx.swing', 'javafx.web' ]
}
dependencies {
    compile fileTree(dir: project.ext.libDir, include: ['*.jar'])
    compile project(':biz.ganttproject.core')
    compile files('data/resources')

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.+'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.+'


    compile 'commons-beanutils:commons-beanutils:1.8.0'
    compile 'commons-codec:commons-codec:1.10'
    compile 'commons-collections:commons-collections:3.2.1'
    compile 'commons-io:commons-io:1.4'
    compile 'commons-lang:commons-lang:2.6'
    compile 'commons-logging:commons-logging:1.2'

    compile 'com.beust:jcommander:1.17'
    compile 'com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.2'

    compile 'com.jgoodies:jgoodies-common:1.7.0'
    compile 'com.jgoodies:jgoodies-looks:2.5.3'

    compile 'de.jensd:fontawesomefx-commons:9.1.2'
    compile 'de.jensd:fontawesomefx-fontawesome:4.7.0-11'

    compile 'net.java.balloontip:balloontip:1.2.4.1'
    compile 'net.sf.ezmorph:ezmorph:1.0.6'

    compile 'org.apache.httpcomponents:httpclient:4.5.6'
    compile 'org.apache.httpcomponents:httpcore:4.4.10'
    compile 'org.apache.httpcomponents:httpmime:4.5.6'
    compile 'org.jdom:jdom:1.1.+'
    compile 'org.swinglabs:jxlayer:3.0.4'


    compile group: 'org.controlsfx', name: 'controlsfx', version: '9.0.+'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.+'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.+'
    compile group: 'org.apache.poi', name: 'poi', version: '3.17'
    compile group: 'org.nanohttpd', name: 'nanohttpd', version: '2.3.+'
    compile group: 'net.sf.json-lib', name: 'json-lib', version: '2.4', classifier: 'jdk15'
    compile group: 'xml-apis', name: 'xml-apis', version: '2.0.2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.+'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.+'
    compile group: 'com.evanlennick', name:'retry4j', version:'0.+'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDirs = ["src"]
            include "**/*.css"
        }
    }

    main.kotlin.srcDirs += 'src'
}


task updateVersion {
    doFirst {
        def gpVersionFile = new File("${projectDir}/src/net/sourceforge/ganttproject/GPVersion.java")
        def gpversion = gpVersionFile.getText('UTF-8')
                .replaceAll('.*BUILD.NUMBER.*', "  public static String BUILD = \"${rootProject.buildNum}\"; // BUILD NUMBER")
        gpVersionFile.write(gpversion, 'UTF-8')
    }
}


task copyPluginFiles {
    copy {
        into(new File(rootProject.pluginsDir, project.name))
        from(jar.outputs.getFiles().getFiles().flatten())
        from(fileTree(".")) {
            include "plugin.xml"
            include "data/resources/**.ttf"
            include "data/resources/**.properties"
            include "data/resources/calendar/**"
            include "data/resources/icons/**"
            include "data/resources/language/**"
            include "data/resources/xslfo/**"
            include "data/resources/xslt/**"
        }
    }
    copy {
        into(new File(rootProject.pluginsDir, "${project.name}/lib/core"))
        from(configurations.compileClasspath) {
            include "*.jar"
        }
    }
}

task copyEclipsito(type: Copy) {
    into(rootProject.distBinDir)
    from(fileTree("lib/core")) {
        include "eclipsito.jar"
    }
}

task copyPlugin(dependsOn: ['copyPluginFiles', 'copyEclipsito']) {
    doFirst {
        println "Copying $project.name to $rootProject.pluginsDir"
    }
}
